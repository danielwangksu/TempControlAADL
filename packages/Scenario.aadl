package Scenario
public
	with Processes, Devices;
	
system Controller
	
end Controller;

system implementation Controller.imp
  	subcomponents
  	-- processes		
		alarmProc : process Processes::AlarmProcess.imp;
		tempProc : process Processes::TempControlProcess.imp;
		fanProc : process Processes::FanProcess.imp;
		tempSensProc : process Processes::TempSensorProcess.imp;
		webInterface : process Processes::WebInterface.imp;
  	-- devices
		alarm : device Devices::Alarm;
		fan : device Devices::Fan;
		tempSensor : device Devices::TempSensor;
	connections
	-- inter-process communication
		t_t : port tempSensProc.current_temp -> tempProc.current_temp;
		t_a : port tempProc.alarm_cmd -> alarmProc.alarm_cmd;
		a_t : port alarmProc.alarm_ack -> tempProc.alarm_ack;
		t_f : port tempProc.fan_cmd -> fanProc.fan_cmd;
		f_t : port fanProc.fan_ack -> tempProc.fan_ack;
		w_t : port webInterface.desired_temp -> tempProc.desired_temp;
		t_w : port tempProc.desired_temp_ack -> webInterface.desired_temp_ack;
		t_s : port tempProc.status -> webInterface.status;
  	-- process to sensors/actuators	
		a_t_a : port alarmProc.alarm_out -> alarm.alarm_control;
		t_t_t : port fanProc.fan_out -> fan.fan_control;
		s_t_s :	port tempSensor.sensor_data -> tempSensProc.sensor_input;
end Controller.imp;

end Scenario;